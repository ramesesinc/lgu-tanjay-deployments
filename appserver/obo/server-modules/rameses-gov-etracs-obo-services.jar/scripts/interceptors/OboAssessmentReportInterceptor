import com.rameses.annotations.*; 

class OboAssessmentReportInterceptor {
	
	@Service("DateService")
	def dateSvc;

	@DataContext("obo_app_fee")
	def feeEm;

	@DataContext("vw_obo_app")
	def vwAppEm;

	@After(pattern="FormReportService.getData", eval="#{ args[0].reportid == 'obo_assessment' }")
	public void getOboAssessmentReport( def evt ) {
		def p = evt.args[0];
		def result = evt.result;
		def reportid = p.reportid;
		def objid = p.parameters.objid;
		if(!objid) throw new Exception("objid is required in assessment report")

		def data = vwAppEm.find( [objid: objid ]).first();
		if(!data) throw new Exception("App not found");

		data.permittype =  data.doctitle ;

		def fitems = feeEm.find([appid: objid]).list();
		def unmappedList = fitems.findAll{ !it.item?.objid };
		if(unmappedList) {
			throw new Exception("Please map the ff. items first /n" + unmappedList*.title.join("/n") );
		}

		//group by fee group
		def vitems = [];
		fitems.groupBy{ it.item.item }.each {k,v->
			def feegroup = v.find{ it.item.feegroupid!=null }.item.feegroup;
			def m = [ 
				item: k, 
				section: feegroup?.title,
				grouporder: feegroup?.sortindex,
				sortorder: v.max{ it.item.sortorder }.item.sortorder, 
				amount: v.sum{it.amount} 
			];
			vitems << m;
		};

		data.items = vitems.sort{ ( (it.grouporder==null?0:it.grouporder) * 1000 )+ (it.sortorder==null? 0: it.sortorder) };

		data.dtfiled = dateSvc.getServerDate();
		result.data = data;
	}	

	@Before(pattern="JasperEmailQueueService.send", eval="#{ args[0].reportid == 'obo_assessment' }")
	public void beforeSendEmail( def evt ) {
		def p = evt.args[0];
		def reportid = p.reportid;
		def ea = vwAppEm.find( [objid: p.refid ] ).first();
		if ( !p.to ) p.to = ea.contact.email;
		p.info = ea;
	}

	@Before(pattern="JasperReportEmailService.send", eval="#{ args[0].reportid == 'obo_assessment' }", index=-10000)
    public void emailAssessment(def evt) {
        def o = evt.args[0];
        def refid = o.refid;
        o.attachments = [
            [ title: "OrderOfPayment.pdf", reportid: o.reportid, parameters: [objid: refid ], 
            	template: "reports/obo/assessment" ]
        ];
        o.to = o.to;
        o.subject = o.subject;
        o.message = o.message;
        o.connection = "obo-email-server";
    }   


}